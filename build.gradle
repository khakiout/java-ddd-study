buildscript {
    ext {
        springBootVersion = "2.2.4.RELEASE"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "com.github.spotbugs" version "3.0.0"
}
apply plugin: "java"
apply plugin: "eclipse"
// code analysis
apply plugin: "checkstyle"
apply plugin: "pmd"

group = "com.khakiout.study"
version = "2.0.0-SNAPSHOT"
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    spotbugsTest.enabled = false
}

tasks.withType(Pmd) {
    ruleSets = []
    ruleSetFiles = files("${project.rootDir}/config/pmd-custom-ruleset.xml")
    reports {
        xml.enabled = true
        html.enabled = true
    }
    ignoreFailures = false
    pmdTest.enabled = false
}

checkstyle {
    toolVersion "8.12"
}
tasks.withType(Checkstyle) {
    description "Check code standard"
    group "verification"
    configFile file("${project.rootDir}/config/modified_google_checks.xml")
    source fileTree("src/main/java")
    include "**/*.java"
    exclude "**/gen/**"

    classpath = files()
    showViolations true

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    useJUnit {
        exclude "**/*IntegrationTests.class"
    }
}

task integrationTest(type: Test) {
    useJUnit {
        include "**/*IntegrationTests.class"
    }
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("ch.qos.logback.contrib:logback-json-classic:0.1.5")
    implementation("ch.qos.logback.contrib:logback-jackson:0.1.5")
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("commons-httpclient:commons-httpclient:3.1")
    implementation("commons-validator:commons-validator:1.4.0")
    implementation("org.glassfish:javax.el:3.0.0")
    implementation("org.liquibase:liquibase-core")
    runtimeOnly("com.h2database:h2")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("io.projectreactor:reactor-test")
}
